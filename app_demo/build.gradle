if (isModule.toBoolean()) {
    apply plugin: 'com.android.application'
    apply plugin: 'android-aspectjx'//AOP的使用
} else {
    apply plugin: 'com.android.library'

    //如果要在库中使用的话
    //库中使用时请使用R2
    //在库中switch-case判断控件id无法编译，请使用if-else
    apply plugin: 'com.jakewharton.butterknife'
}


android {
    compileSdkVersion build_versions.compile_sdk
    buildToolsVersion build_versions.build_tools

    defaultConfig {
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode 1
        versionName "1.0"

        javaCompileOptions {//用于Route的匹配
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        packagingOptions {
            //解决编译时com.android.builder.merge.DuplicateRelativeFileException: More than one file was found with OS independent path 'META-INF/rxjava.properties'这个错误
            exclude 'META-INF/rxjava.properties'
        }

        ndk {
            moduleName "Hello"
        }
        multiDexEnabled true

    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            if (isModule.toBoolean()) {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                //集成开发模式下排除debug文件夹中的所有Java文件
                java {
                    exclude 'debug/**'
                }
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    annotationProcessor deps.arouter_compiler
    implementation project(path: ':common')

    annotationProcessor deps.butterKnifeCompiler//需要使用的地方都要加，不然绑定失效

//  Top性能优化演示所需依赖框架
    implementation 'org.aspectj:aspectjrt:1.8.14'//AOP的使用
    implementation 'me.weishu:epic:0.3.6'
    implementation 'com.android.support:asynclayoutinflater:28.0.0'
    annotationProcessor 'com.zhangyue.we:x2c-apt:1.1.2'
    implementation 'com.zhangyue.we:x2c-lib:1.0.6'
    implementation 'com.tencent:mmkv:1.0.17'
    api ('com.aliyun.ams:alicloud-android-httpdns:1.1.7@aar') {
        transitive true
    }
    compileOnly 'me.ele:lancet-base:1.0.4'
    implementation 'com.github.markzhai:blockcanary-android:1.5.0'
    implementation 'com.github.anrwatchdog:anrwatchdog:1.3.0'
    implementation 'org.ligboy.retrofit2:converter-fastjson-android:2.1.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'

}
